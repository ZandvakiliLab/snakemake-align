rule star_index:
    input:
        fasta=rules.get_genome.output.fasta,
    output:
        directory("results/star/index/"),
    log:
        "results/star/index/index.log",
    threads: 1
    params:
        extra=config["mapping"]["star"]["index"],
    message:
        "build star index"
    wrapper:
        "v3.3.7/bio/star/index"


rule star_align:
    input:
        fq1=lambda wildcards: get_processed_fastq(wildcards.sample, regex="read1"),
        fq2=lambda wildcards: (
            get_processed_fastq(wildcards.sample, regex="read2")
            if is_paired_end()
            else []
        ),
        idx=rules.star_index.output,
    output:
        aln="results/star/align/{sample}/mapped.bam",
        log_final="results/star/align/{sample}/Log.final.out",
    log:
        "results/star/align/{sample}/mapped.log",
    threads: 8
    params:
        extra=config["mapping"]["star"]["extra"],
    message:
        "make star alignment"
    wrapper:
        "v3.3.7/bio/star/align"
