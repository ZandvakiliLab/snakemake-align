rule bowtie2_build:
    input:
        ref="results/genome/genome.fasta",
    output:
        multiext(
            "results/bowtie2/build/genome",
            ".1.bt2",
            ".2.bt2",
            ".3.bt2",
            ".4.bt2",
            ".rev.1.bt2",
            ".rev.2.bt2",
        ),
    log:
        "results/bowtie2/build/build.log",
    threads: 1
    params:
        extra=config["mapping"]["bowtie2"]["index"],
    message:
        "build bowtie2 index"
    wrapper:
        "v9.4.1/bio/bowtie2/build"


rule bowtie2_align:
    input:
        sample=get_processed_fastq,
        idx=rules.bowtie2_build.output,
    output:
        "results/bowtie2/align/{sample}/mapped.bam",
    log:
        "results/bowtie2/align/{sample}/mapped.log",
    threads: 8
    params:
        extra=config["mapping"]["bowtie2"]["extra"],
    message:
        "make bowtie2 alignment"
    wrapper:
        "v9.4.1/bio/bowtie2/align"
